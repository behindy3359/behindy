plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
def lombokVersion = '1.18.30'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}
dependencies {

	// Spring Core
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// DB Driver
	runtimeOnly 'org.postgresql:postgresql'

	// DevTools
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// Lombok
	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

	// Security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.jasypt:jasypt:1.9.3'

	// xss
	implementation 'org.jsoup:jsoup:1.15.3'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// Redis (토큰 저장용)
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// HTTP 클라이언트 (외부 API 호출용)
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// JSON 처리 향상
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'com.fasterxml.jackson.module:jackson-module-parameter-names'

	// Swagger/OpenAPI (Spring Boot 3.4+ 호환 버전)
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// H2 Database (테스트용)
	testImplementation 'com.h2database:h2'
}

// 테스트 활성화 및 설정
tasks.withType(Test).configureEach {
	enabled = true
}

test {
	useJUnitPlatform()

	testLogging {
		events "passed", "skipped", "failed"
		exceptionFormat "full"
	}

	ignoreFailures = false
	maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

// JaCoCo 설정
jacoco {
	toolVersion = "0.8.11"
}

jacocoTestReport {
	dependsOn test

	reports {
		xml.required = true
		html.required = true
		csv.required = true
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				'**/config/**',
				'**/dto/**',
				'**/entity/**',
				'**/exception/**',
				'**/*Application.class'
			])
		}))
	}
}

jacocoTestCoverageVerification {
	enabled = false  // 커버리지 검증을 비활성화 (리포트는 생성되지만 빌드 실패 안 함)
	violationRules {
		rule {
			limit {
				minimum = 0.70
			}
		}
	}
}